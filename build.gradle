plugins {
    id 'com.gorylenko.gradle-git-properties' version '2.2.4'
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.ben-manes.versions' version '0.38.0'
	id 'eclipse'
	id 'idea'
	id 'java'
    id 'jacoco'
    id 'com.google.cloud.tools.jib' version '2.8.0'
    id 'com.github.spotbugs' version '4.7.0'
    id 'info.solidsoft.pitest' version '1.6.0'
    id "org.sonarqube" version "3.0"
	id "com.github.pivotalservices.cf-app" version "2.3.0-rc.8"
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}

spotbugs {
    toolVersion = '4.2.1'
    ignoreFailures = true
    effort = 'max'
    reportLevel = 'low'
    showProgress = true
}

spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.xml")
        }
    }
}

group = 'io.pivotal'
version = '1.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


repositories {
	mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
    maven { url "https://jitpack.io" }
	maven { url "http://clojars.org/repo/" }
}

configurations {
	all {
    	exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
	}
	runtimeAgent
}

dependencyManagement {
	imports {
		mavenBom "com.microsoft.azure:azure-spring-boot-bom:2.3.3"
	}
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
	doFirst {
		// Ensure that all of the agents we need to load at run-time happen when running the app through
		// Gradle
		configurations.runtimeAgent.each {
			File jarFile ->
				jvmArgs "-javaagent:${jarFile.absolutePath}"
		}
    }
}

test {
	doFirst {
		// Ensure that all of the agents we need to load at run-time happen when running the app through
		// Gradle
		configurations.runtimeAgent.each {
			File jarFile ->
				jvmArgs "-javaagent:${jarFile.absolutePath}"
		}
    }
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
        exceptionFormat "full"
        showCauses true
        showExceptions true
        showStackTraces true
	}
}

java {
    withJavadocJar()
    withSourcesJar()
}

jacoco {
	toolVersion = "0.8.6"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
		csv.enabled = false
	}
}

pitest {
    testPlugin = "junit5"
    jvmArgs = ['-Xmx1024m']
    threads = 5
    targetClasses = ['io.pivotal.cfapp.*']
    outputFormats = ['XML', 'HTML']
}

jar {
	baseName 'track-shipments'
	excludes = ['**/application.yml']
}

sonarqube {
    properties {
        property "sonar.projectName", "Track Shipments"
        property "sonar.projectKey", "io.pivotal:track-shipments"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

cfConfig {
	//CF Details
	ccHost = "api.local.pcfdev.io"
	ccUser = "admin"
	ccPassword = "admin"
	org = "pcfdev-org"
	space = "pcfdev-space"

	//App Details
	name = "track-shipments"
	host = "track-shipments"
	filePath = "${buildDir}/libs/track-shipments-${version}.jar"
	path = ""
	domain = "local.pcfdev.io"
	instances = 1
	memory = 1024

	//Env and services
	buildpack = "https://github.com/cloudfoundry/java-buildpack"
	environment = ["JAVA_OPTS": "-Djava.security.egd=file:/dev/./urandom", "SPRING_PROFILES_ACTIVE": "cloud"]
}

dependencies {
	// Required top-level dependency to support AOP and the AspectJ Transformer in Spring, even
    // when using Load-Time Weaving (LTW).
    implementation "org.springframework.boot:spring-boot-starter-aop"
	implementation('com.microsoft.azure:azure-spring-boot')
	implementation('com.microsoft.sqlserver:sqljdbc4:4.0')
	implementation('org.apache.commons:commons-lang3:3.10')
	implementation('org.springframework.boot:spring-boot-starter-actuator')
	implementation('org.springframework.boot:spring-boot-starter-data-jpa') { 
		exclude group: 'org.hibernate', module: 'hibernate-core'
		exclude group: 'org.hibernate', module: 'hibernate-entitymanager' 
	}
	implementation('org.eclipse.persistence:org.eclipse.persistence.jpa:2.7.7')
	implementation('org.springframework.boot:spring-boot-starter-data-rest')
	implementation('org.springframework.data:spring-data-rest-hal-browser')
	implementation('org.springframework.boot:spring-boot-starter-hateoas')
	implementation('org.springframework.boot:spring-boot-starter-log4j2')
	implementation('org.springframework.boot:spring-boot-starter-web')
	implementation('com.fasterxml.uuid:java-uuid-generator:4.0.1')
	runtime('org.springframework.boot:spring-boot-devtools')
	runtime('com.h2database:h2')
	runtime('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.2')
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	    exclude group: 'junit', module: 'junit'
    }
    testImplementation('org.pitest:pitest-junit5-plugin:0.12')
	
	// BEGIN: Required for LTW to work when the application is run via the "test" or bootRun" Gradle
    //        tasks.
    runtimeAgent "org.springframework:spring-instrument"
    // END
	
}

project.tasks.withType(JavaCompile) {
    options.fork = true
    options.compilerArgs << '-parameters'
}
