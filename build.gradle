buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
		cfAppVersion = '2.1.0'
	}
	repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/releases" }
		maven { url "https://repo.spring.io/release" }
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
      	classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1")
		classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.17")
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.github.pivotalservices:ya-cf-app-gradle-plugin:${cfAppVersion}")
	}
}


apply plugin: 'cf-app'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'

group = 'io.pivotal'
version = '1.0-SNAPSHOT'
sourceCompatibility = 1.8


gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "UTC"
}

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
	reports {
	    xml.enabled = true
	    html.enabled = true
	    csv.enabled = false
    }
}

check.dependsOn jacocoTestReport

jar {
	baseName 'track-shipments'
	excludes = ['**/application.yml']
}

task execJar (type: Jar, dependsOn: jar) {
	baseName 'track-shipments'
    classifier = 'exec'
    from sourceSets.main.output
}

sonarqube {
    properties {
        property "sonar.projectName", "Track Shipments"
        property "sonar.projectKey", "io.pivotal:track-shipments"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

repositories {
	mavenCentral()
}

project.tasks.withType(JavaCompile) {
    options.fork = true
    options.compilerArgs << '-parameters'
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

configurations {
	all {
    	exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
	}
    instrument { 
        description 'classpath for instrument as java agent' 
    }
}

cfConfig {
	//CF Details
	ccHost = "api.local.pcfdev.io"
	ccUser = "admin"
	ccPassword = "admin"
	org = "pcfdev-org"
	space = "pcfdev-space"

	//App Details
	name = "track-shipments"
	host = "track-shipments"
	filePath = "${buildDir}/libs/track-shipments-${version}-exec.jar"
	path = ""
	domain = "local.pcfdev.io"
	instances = 1
	memory = 1024

	//Env and services
	buildpack = "https://github.com/cloudfoundry/java-buildpack"
	environment = ["JAVA_OPTS": "-Djava.security.egd=file:/dev/./urandom", "SPRING_PROFILES_ACTIVE": "cloud"]
}

// vulnerability protection
dependencyManagement {
	dependencies {
		dependencySet(group:'com.fasterxml.jackson.core', version: '2.8.11') {
			entry 'jackson-annotations'
			entry 'jackson-databind'
		}
		dependencySet(group: 'com.fasterxml.jackson.datatype', version: '2.8.11') {
			entry 'jackson-datatype-jsr310'
		}
	}
}

dependencies {
	compile('org.apache.commons:commons-lang3:3.7')
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa') { exclude module: 'hibernate-entitymanager' }
	compile('org.eclipse.persistence:org.eclipse.persistence.jpa:2.7.0')
	compile('org.springframework.boot:spring-boot-starter-data-rest')
	compile('org.springframework.data:spring-data-rest-hal-browser')
	compile('org.springframework.boot:spring-boot-starter-hateoas')
	compile('org.springframework.boot:spring-boot-starter-log4j2')
	//compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('com.fasterxml.uuid:java-uuid-generator:3.1.4')
	compile('com.google.guava:guava:20.0')
	runtime('org.springframework.boot:spring-boot-devtools')
	runtime('com.h2database:h2')
	runtime('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
	compileOnly('org.projectlombok:lombok')
	instrument('org.springframework:spring-instrument')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	//testCompile('org.springframework.security:spring-security-test')
}

test { 
    jvmArgs "-javaagent:$project.configurations.instrument.singleFile"
}

bootRepackage  {
    withJarTask = tasks['execJar']
}

bootRun {
    jvmArgs "-javaagent:$project.configurations.instrument.singleFile"
    systemProperties = System.properties
}

task copySpringInstrumentJar (type:Copy) {
    description = 'Copies the spring-instrument.jar into your lib directory.'
    outputs.file project.file('lib/spring-instrument.jar')
    from (project.configurations.instrument)
    into("lib")
    rename { String filename -> filename = 'spring-instrument.jar' }
}

tasks.idea.dependsOn(project.tasks.copySpringInstrumentJar)
tasks.eclipse.dependsOn(project.tasks.copySpringInstrumentJar)
tasks.processResources.dependsOn(project.tasks.copySpringInstrumentJar)
